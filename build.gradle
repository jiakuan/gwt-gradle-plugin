import org.gradle.plugins.ide.eclipse.model.AbstractLibrary
import org.gradle.plugins.ide.eclipse.model.ClasspathEntry

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
  }
}

plugins {
  id 'java'
  id 'maven-publish'
  id 'signing'
  id 'eclipse'

  // https://plugins.gradle.org/plugin/net.researchgate.release
  id 'net.researchgate.release' version '3.0.0'

  // For publishing the plugin to the Gradle Plugin Portal
  id 'java-gradle-plugin'

  // https://plugins.gradle.org/plugin/com.gradle.plugin-publish
  id "com.gradle.plugin-publish" version "1.0.0"
}

group = 'org.docstr'
version = "$version"

def javaVersion = JavaVersion.VERSION_1_8
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories { mavenCentral() }

dependencies {
  implementation gradleApi()
  testImplementation group: 'junit', name: 'junit', version: '4.11'
}

ext {
  encoding = 'UTF-8'
}

// Change the output directory for the main source set back to the old path
sourceSets.main.java.outputDir = new File(buildDir, "classes/main")
sourceSets.test.java.outputDir = new File(buildDir, "classes/test")

pluginBundle {
  website = 'http://gwt-gradle-plugin.documentnode.io'
  vcsUrl = 'https://github.com/jiakuan/gwt-gradle-plugin'
  tags = ['gwt', 'gwt-gradle']
}

gradlePlugin {
  plugins {
    gwtGradlePlugin {
      id = 'org.docstr.gwt'
      displayName = 'GWT Gradle Plugin'
      description = 'Gradle plugin to support GWT related tasks.'
      implementationClass = 'org.docstr.gradle.plugins.gwt.GwtPlugin'
    }
    gwtBaseGradlePlugin {
      id = 'org.docstr.gwt.base'
      displayName = 'GWT Base Gradle Plugin'
      description = 'Gradle plugin to support GWT related tasks.'
      implementationClass = 'org.docstr.gradle.plugins.gwt.GwtBasePlugin'
    }
    gwtCompilerGradlePlugin {
      id = 'org.docstr.gwt.compiler'
      displayName = 'GWT Compiler Gradle Plugin'
      description = 'Gradle plugin to support GWT related tasks.'
      implementationClass = 'org.docstr.gradle.plugins.gwt.GwtCompilerPlugin'
    }
    gwtWarGradlePlugin {
      id = 'org.docstr.gwt.war'
      displayName = 'GWT War Gradle Plugin'
      description = 'Gradle plugin to support GWT related tasks.'
      implementationClass = 'org.docstr.gradle.plugins.gwt.GwtWarPlugin'
    }
  }
}

eclipse {
  classpath {
    file {
      whenMerged { org.gradle.plugins.ide.eclipse.model.Classpath cp ->
        String gradleHome = gradle.getGradleHomeDir().absolutePath.replace(File.separator, '/')
        String gradleSrc = "${gradleHome}/src"
        cp.entries.each { ClasspathEntry entry ->
          if ((entry in AbstractLibrary) && (entry.library.file.name.startsWith('gradle-'))) {
            entry.sourcePath = new org.gradle.plugins.ide.eclipse.model.internal.FileReferenceFactory().fromPath(gradleSrc)
          }
        }
      }
    }
  }
}

ext.userHome = System.getProperty("user.home")

javadoc {
  // Avoid error: cannot access external classes
  options.addStringOption("sourcepath", "")

  if (JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}

java {
  withJavadocJar()
  withSourcesJar()
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

signing {
  sign publishing.publications
}

processResources {
  duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

def ossrU = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
def ossrP = project.hasProperty("ossrhPassword") ? ossrhPassword : ""

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }
      pom {
        name = 'GWT Gradle Plugin'
        description = 'Gradle plugin to support GWT (http://www.gwtproject.org/) related tasks.'
        url = 'https://github.com/jiakuan/gwt-gradle-plugin/'
        packaging = 'jar'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'jiakuan'
            name = 'Jiakuan Wang'
            email = 'jake@documentnode.io'
            organization = 'Document Node'
            organizationUrl = 'https://documentnode.io'
          }
        }

        scm {
          connection = 'scm:git:git@github.com:jiakuan/gwt-gradle-plugin.git'
          developerConnection = 'scm:git:git@github.com:jiakuan/gwt-gradle-plugin.git'
          url = 'git@github.com:jiakuan/gwt-gradle-plugin.git'
        }
      }
    }
  }
  repositories {
    maven {
      def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
      credentials {
        username = ossrU
        password = ossrP
      }
    }
  }
}

tasks.eclipse.doLast {
  File prefs = project.file('.settings/org.eclipse.core.resources.prefs');
  if (!prefs.exists()) {
    prefs.parentFile.mkdirs()
    project.file('.settings/org.eclipse.core.resources.prefs') << "eclipse.preferences.version=1\nencoding/<project>=$encoding\n";
  }
}

tasks['eclipse'].doLast {
  mkdir 'build/pluginUnderTestMetadata'
}
